/*

Comments:
(T1) - content of tutorial "Part 1: Automatic Transition of NPCs into SoD and between Camps in SoD"
(T2) - content of tutorial "Part 2: Make Your NPC Comment and Move Along at the End of SoD"
(T4) - content of tutorial "Part 4: SoD Banter System for Your NPC"


*/

BACKUP ~xxBiff/backup~ //put the mod folder name here, for the rest of the mod, you can use %MOD_FOLDER% (decreases the amount of copy&paste errors...)

AUTHOR ~https://www.gibberlings3.net/forums/topic/30139-jasteys-sod-npc-modding-tutorials/~ //The info given here will be shown if the install fails with the note to send the DEBUG here for helfile p. Put here wherever you want the player to seek your help in case the installation fails. I used to give my email address but switched to giving a forum link as it has several advantages.

VERSION ~4~ //version number. Always give a specific version number to any mod version you make public, or debugging a player's install will be difficult.

README ~xxBiff/readme.Biff.%LANGUAGE%.txt~ //The file specified here will be called after the language call, but before the installation of the mod. Can also be html.

AUTO_TRA ~xxBiff/tra/%s~ /* This is where the installer looks for text strings, the "%s" is for the chosen language version. 
Please note: text files for the EE need to be in utf8 without BOM. However, in this tutorial mod the tra files are in ANSI. This is because so far, the usual way of handling tra files in mod installation was direct conversion from ANSI to utf8 using HANDLE_CHARSETS with iconv.exe. This is the way how it is done here. */
/* please note: the tras are called in the order: 
LANGUAGE, WITH_TRA, USING and AUTO_TRA
with the last one overwriting the first, meaning: you can overwrite strings called by LANGUAGE with WITH_TRA, both will be overwritten by USING, but if there is a tra file of the same name of the file you are compiling in the AUTO_TRA folder, the strings with matching numbers from there will be taken with highest priority. */

ALWAYS //put stuff that should be done by every install of every component here. 

/* this is needed if ingame text ressources need to be patched e.g. inside a script so the text matches 100%: needed for exact match in REPLACE_TEXTUALLY */
  INCLUDE ~%MOD_FOLDER%/lib/extra_regexp_vars.tph~
  OUTER_SPRINT newline ~%WNL%%LNL%%MNL%%TAB% ~

  ACTION_DEFINE_ARRAY traxxreload BEGIN game-xxbiff END //game.tra contains ingame strings so it needs to be reloaded after the utf-8 conversion
//  ACTION_DEFINE_ARRAY traxxnoconvert BEGIN END //if you have a tra file that doesn't need to be reloaded (e.g. a pure setup.tra for the install process only) or that is already in utf-8 format and shouldn't be included for conversion, you could specify it here

  LAF HANDLE_CHARSETS
	INT_VAR
		infer_charsets = 1 //choses the correct encoding for different languages
	STR_VAR
		tra_path = EVAL ~%MOD_FOLDER%/tra~ //path to the LANGUAGE folders
//		noconvert_array = traxxnoconvert //if you specified the array above
		reload_array = traxxreload //loads the array defined above
		iconv_path = EVAL ~%MOD_FOLDER%/tra/iconv~ //path to the inconv folder
  END

END

/* language choices the player gets to chose from upon install. The tra files needed for the tp2 need to be read in here. Note: any ingame texts need to be reloaded after the conversion to utf8 as it is done above. */
LANGUAGE ~English~ ~english~ ~xxbiff/tra/english/setup-xxbiff.tra~ ~xxbiff/tra/english/game-xxbiff.tra~

/* commented out as it's just an example. Ingame texts are not translated.
LANGUAGE ~German~ ~german~ ~xxbiff/tra/german/setup-xxbiff.tra~ 
~xxbiff/tra/german/game-xxbiff.tra~
*/

BEGIN @90000 /* ~Biff the Understudy: joinable "tutorial" NPC for SoD~ */ DESIGNATED 0 
/* giving the component a DESIGNATED number which will not change in the future is of great importance for crossmod compatibility. In this case, it would have been the "0th" component anyway, but especially for other components (if your mod has them), this would be important. */
LABEL xxbiff_sod_tutorial_mod-main /* define a unique global label for this mod component(!). It basically does what DESIGNATED does, only better. And it helps Project Infinity remember the component when setting up an install list. */

/* make sure the mod cannot be installed to a wrong game, like BGII etc. */
/* Biff can only be installed to BG:SoD and EET. */
REQUIRE_PREDICATE (GAME_IS ~bgee eet~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) @90001 /* ~This mod is really only for SoD~ */

/* Biff's cre file */
/* this tutorial is not a good example for how to create your cre-file etc. I just took the exsiting Biff the Understudy (DIALOGMO.CRE) and adjusted his Allegiance, his script name (death variable), his override script and his greetings dialogue.  */

COPY ~%MOD_FOLDER%/cre/xxbiff.cre~ ~override~
//SAY NAME1 #11662 //we don't need this for Biff as his name is in the dialog.tlk already
//SAY NAME2 #11662
/* Biff doesn't have a sound set (yet)
SAY INITIAL_MEETING ~~
SAY MORALE ~~   
SAY HAPPY ~~        
SAY UNHAPPY_ANNOYED ~~   
SAY UNHAPPY_SERIOUS ~~   
SAY UNHAPPY_BREAKING ~~   
SAY LEADER ~~   
SAY TIRED ~~   
SAY BORED ~~ 
SAY BATTLE_CRY1 ~~  
SAY BATTLE_CRY2 ~~   
SAY BATTLE_CRY3 ~~   
SAY BATTLE_CRY4~~   
SAY DAMAGE ~~
SAY DYING ~~
SAY HURT ~~   
SAY AREA_FOREST ~~   
SAY AREA_CITY ~~  
SAY AREA_DUNGEON ~~   
SAY SELECT_COMMON1 ~~   
SAY SELECT_COMMON2 ~~   
SAY SELECT_COMMON3 ~~   
SAY SELECT_COMMON4 ~~   
SAY SELECT_COMMON5 ~~   
SAY SELECT_COMMON6 ~~   
SAY SELECT_ACTION1 ~~   
SAY SELECT_ACTION2 ~~   
SAY SELECT_ACTION3 ~~   
SAY SELECT_ACTION4 ~~   
SAY SELECT_ACTION5 ~~   
SAY SELECT_ACTION6 ~~   
SAY SELECT_ACTION7 ~~   
SAY SELECT_RARE1 ~~   
SAY SELECT_RARE2  ~~   
SAY CRITICAL_HIT ~~   
SAY CRITICAL_MISS ~~   
SAY TARGET_IMMUNE ~~   
SAY INVENTORY_FULL ~~ 
SAY BIO ~~
*/


/* Biff's dialogues */

/* (T1) SoD greeting dialogue */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogue/xxBiffs_greetingsdialogue.d~

//actually, we do not really need EVALUATE_BUFFER as long as the file doesn't use any OUTER_SPRINT variables etc. but it doesn't hurt, either.

/* (T1) SoD kickout dialogue */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogue/xxBiffsP_kickoutdialogue.d~

/* (T2/(later also T3)) Biff's comments on SoD events */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogue/sod_event_comments.d~


/* script compilation */

// (T4) Biff's SoD script xxBiffs.baf 
COMPILE ~%MOD_FOLDER%/baf/xxBiffs.baf~

/* (T4) banter script with Corwin (and Minsc) */
COMPILE ~%MOD_FOLDER%/baf/xxBiff01.baf~


/* script compilation: modify existing scripts */

/* (T1) transition from BG:EE to SoD: change Biff's Joined dialogue and OVERRIDE script */
/* +++ Biff doesn't really need this since he won't be available before the transition to the city. This is for demonstration purposes if your NPC carries over from BG:EE +++ */
EXTEND_TOP ~bdintro.bcs~ ~%MOD_FOLDER%/baf/xxbdintro.baf~
  EVALUATE_BUFFER

/* (T1) after Korlasz is defeated: PC is moved to Ducal Palace. Joined NPCs leave */
/* +++ Biff doesn't really need this since he won't be available before the transition to the city. This is for demonstration purposes if your NPC carries over from BG:EE +++ */
EXTEND_TOP ~bd0103.bcs~ ~%MOD_FOLDER%/baf/xxbd0103.baf~
  EVALUATE_BUFFER 

/* (T1) let Biff (re)appear in front of the Duchal Palace */
EXTEND_BOTTOM ~bd0010.bcs~ ~%MOD_FOLDER%/baf/xxbd0010.baf~ EVALUATE_BUFFER

/* (T1) Biff is in front of Ducal Palace if told to wait - or Player marches onto the battle without recruiting NPCs in Baldur' Gate: Biff will be in front of the palace, too */
EXTEND_BOTTOM ~bd0101.bcs~ ~%MOD_FOLDER%/baf/xxbd0101.baf~
  EVALUATE_BUFFER

/* (T1) Biff moves to war camp in bd1000.are */
EXTEND_BOTTOM ~bd1000.bcs~ ~%MOD_FOLDER%/baf/xxbd1000.baf~
  EVALUATE_BUFFER

/* (T1) and the same for the next camp in bd7100.are: */
EXTEND_BOTTOM ~bd7100.bcs~ ~%MOD_FOLDER%/baf/xxbd7100.baf~
  EVALUATE_BUFFER

/* (T1) Last camp is in bd3000.are. In case Biff was never in party or kicked out to wait somewhere else, he will also be back in the (new) camp. */
EXTEND_BOTTOM ~bd3000.bcs~ ~%MOD_FOLDER%/baf/xxbd3000.baf~
  EVALUATE_BUFFER

/* (T1) return to camp if kicked out */
EXTEND_TOP ~bdparty.bcs~ ~%MOD_FOLDER%/baf/xxbdparty.baf~
  EVALUATE_BUFFER

/* (T1) Biff is kicked in hell dimension - teleport to last hell area bd4400.are */
EXTEND_TOP ~bdcut58.bcs~ ~%MOD_FOLDER%/baf/xxbdcut59b.baf~
  EVALUATE_BUFFER

/* (T1) Biff is kicked in hell dimension or was waiting elsewhere- teleport to Dragonspear Castle interior (bd4300.are) after PC returns from Avernus */
EXTEND_TOP ~bdcut59b.bcs~ ~%MOD_FOLDER%/baf/xxbdcut59b.baf~
  EVALUATE_BUFFER

/* (T2) Biff comments on murder accusation */
EXTEND_TOP ~bd4100.bcs~ ~%MOD_FOLDER%/baf/xxbd4100.baf~
  EVALUATE_BUFFER

/* (T2) after PC gets arrested for murder: Biff leaves party */
EXTEND_TOP ~bdcut61.bcs~ ~%MOD_FOLDER%/baf/xxbdcut61.baf~
  EVALUATE_BUFFER

/* (T2) Biff visits PC in Prison */
EXTEND_TOP ~bd0104.bcs~ ~%MOD_FOLDER%/baf/xxbd0104.baf~
  EVALUATE_BUFFER

/* (T2) Biff waits in front of the prison exit */
EXTEND_TOP ~bd6200.bcs~ ~%MOD_FOLDER%/baf/xxbd6200.baf~
  EVALUATE_BUFFER

/* [DEPRECATED] - refer to Tutorial 2 "Make Your NPC Comment and Move Along at the End of SoD"
/* (T2) Let Biff join after PC escaped from prison (if there is still a free slot in the group) */
EXTEND_TOP ~bdcut64x.bcs~ ~%MOD_FOLDER%/baf/xxbdcut64x.baf~
  EVALUATE_BUFFER
*/

/* [DEPRECATED] - refer to Tutorial 2 "Make Your NPC Comment and Move Along at the End of SoD"
/* (T2) Biff should move with the group in the last area (abduction scene, bd6100.are) */
COPY_EXISTING ~bdcut65.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN

		SPRINT textToReplace ~\(SetAreaScript("",OVERRIDE)\)[%newline%]*\(END\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 
\2 

IF
	Global("xxBiff_SoDEndMove","bd6100",0) 
	InMyArea("xxBiff")
	!Dead("xxBiff")  
	!InParty("xxBiff")  
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SetGlobal("xxBiff_SoDEndMove","bd6100",1) 
		ActionOverride("xxBiff",MoveToPoint([930.795])) //you can leave these coordinates for your NPC, too
END
~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY_IF_IT_CHANGES
*/



/* bddialog.2da patching */
/* by patching the bddialog.2da the engine knows which dialogues to call in SoD for Biff for joined and kickout dialogue and the dream script (triggers upon resting) */

/* add Biff's dialogues and dream script to bddialog.2da */
APPEND ~bddialog.2da~
~xxBiff xxBiffsP xxBiffJ xxBiffD~ //first entry if Biff's death variable
UNLESS ~xxBiff~

/* (T1) EET compatibility: Biff is an BG1 (SoD) NPC only. His name will be mentioned on EET's fate spirit in ToB, but he will not be summonable. */

ACTION_IF GAME_IS ~eet~ BEGIN
  INCLUDE ~EET/other/EET_functions.tph~
  LAF ~EET_NPC_TRANSITION~ 
    INT_VAR
      type = 1 //Biff is only available in BG:EE/SoD
    STR_VAR
      dv = "xxBiff" 
      override_BG1 = "" /* OVERRIDE script in BG:EE. Put your NPC's BG:EE script here UNLESS you use the same script for BG:EE and SoD 
(adds a "destruct in Chapter greater than 7" scriptblock, for example if your NPC was spawned in BG:EE but was never in party (although "BeenInParty" is not being checked by this scriptblock)). 
If your NPC is SoD only like Biff, leave this open. */
      override_SoD = "xxBiffs" //OVERRIDE script in SoD. This will add a "Selfdestruct" in Chapter greater than 13 to the SoD script.
      traFile = "%MOD_FOLDER%/tra/%LANGUAGE%/dialog.tra" //path to the text line for the ToB Fate Spirit summoning
      string = "@0" /* ~Bring me Biff the Understudy.~ */
      stringPosDV = "Baeloth" //Name after which Biff's should be put into the order of reply options at the fate spirit summoning dialogue. 
/* From the "Modder's Notes for Baldur's Gate: Enhanced Edition Trilogy (EET)":
    stringPosDV = "" //set the DV of NPC right below your response (used for sorting FATESP responses alphabetically)
      //Aerie, Ajantis, Alora, Anomen, Baeloth, Branwen, Cernd, Coran, Corwin, Dorn, Dynaheir, Edwin, Eldoth, Faldorn, Garrick,
      //Glint, HaerDalis, Hexxat, Imoen2, Jaheira, Jan, Kagain, Keldorn, Khalid, Kivan, Korgan, MKhiin, Mazzy, Minsc, Montaron,
      //Nalia, Neera, Quayle, Rasaad, Safana, SharTeel, Skie, Tiax, Valygar, Viconia, Voghiln, Wilson, Xan, Xzar, Yeslick, Yoshimo
      //variable not set (default) = NPC name appended at the end of summoning list */
  END
END

