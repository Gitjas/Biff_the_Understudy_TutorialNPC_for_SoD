/* xxbdparty.baf:
Addition to bdparty.bcs to toggle moving back to the current camp if kicked out of party. */
/* we do not add "Continue()" here as it might leads to bugs for our mod - this is an exception */

IF
  Global("bd_npc_camp","locals",1)  //this is set via the kickout dialogue xxBiffsP.dlg
  Name("xxBiff",Myself)
  Switch("bd_npc_camp_chapter","global")  //this makes sure the NPC goes back to the correct camp along SoD's campaign
  OR(2)
    !Range("ff_camp",999) //"Name" of a trigger which detects the camps
    !TriggerOverride("ff_camp",IsOverMe("xxBiff"))
THEN
  RESPONSE #2
    EscapeAreaMove("bd1000",720,3784,N) //coordinates as used in xxbd1000.baf
  RESPONSE #3
    EscapeAreaMove("bd7100",351,3356,N) //coordinates as used in xxbd7100.baf
  RESPONSE #4
    EscapeAreaMove("bd3000",1552,1723,N) //coordinates as used in xxbd3000.baf
END

/* this sets the "bd_npc_camp" variable to "2" as soon as Biff is in one of the camps again: bg1000.are, bg7100.are, bd3000.are – and will make bdparty.bcs set the correct fighting script etc. */
IF
  GlobalLT("bd_npc_camp","locals",2)
  Global("bd_joined","locals",0)
  Name("xxBiff",Myself)
  TriggerOverride("ff_camp",IsOverMe("xxBiff"))
  Switch("bd_npc_camp_chapter","global")
THEN
  RESPONSE #2
    SetGlobal("bd_npc_camp","locals",2)
    SaveLocation("LOCALS","bd_default_loc",[720.3784]) //coordinates as used in xxbd1000.baf
  RESPONSE #3
    SetGlobal("bd_npc_camp","locals",2)
    SaveLocation("LOCALS","bd_default_loc",[351.3356]) //coordinates as used in xxbd7100.baf
  RESPONSE #4
    SetGlobal("bd_npc_camp","locals",2)
    SaveLocation("LOCALS","bd_default_loc",[1552.1723]) //coordinates as used in xxbd3000.baf
    ChangeAIScript("bdasc3",CLASS)
END