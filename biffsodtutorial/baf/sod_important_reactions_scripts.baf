/* NPC reactions to specific crucial knowledge about Caelar's plans */
/* This template is making use of the variable tracking system of the Road to Discovery mod.
In addition, some reactions are also triggered by game events if the Road to Discovery mod is not installed.
I made the following assumptions in case Road to Discovery is NOT installed:
-The PC knows who Hephernaan is after the parley with Caelar at Coastway Crossing (GlobalGT("bd_plot","global",169) ).
-The PC knows that Caelar wants to free souls from Avernus after the parley at Coastway Crossing (GlobalGT("bd_plot","global",169) ).
-The PC knows that Hephernaan is betraying Caelar after the actual betrayal ( GlobalGT("bd_plot","global",494) ). To check for Hephernaan's mirror scene or what the ghost Daeros tells the group is what Road to Discovery is for.
-Only source to know about Aun Argent being trapped in Avernus BEFORE meeting him is either a mod setting the Road to Discovery variable or the Stat Based Reactions component from Themed Tweaks. 
-The PC will know about Caelar being no Bhaalchild if Caelar told so explicitely at the parley at Coastway Crossing ( BhaalGlobalGT("bd_mdd018","global",0) ).
-The NPC can conclude that Caelar wants to open a portal to Avernus after the parley at Coastway Crossing AND if the group found the treatise (bdbook11).
-PC will know that Caelar is planning on opening a portal to Avernus after the parley at Dead Man's Pass.
-The NPC can conclude that Caelar needs the PC's blood if they found the treatise (bdbook11), know that Caelar is no Bhaalchild, and know that she is planning on opening a portal.

The trigger work in the following way:
-If Road to Discovery is installed (i.e. Global("C#RtD_RoadToDiscovery","GLOBAL",1) ), ONLY the variables of its tracking system should give true.
-If Road to Discovery is not installed (i.e. Global("C#RtD_RoadToDiscovery","GLOBAL",0) ), the ingame triggers listed above should be used.

This template has two kind of reactions: CONCLUSIONS and general REACTIONS.
-Conclusions are more like adding 1+1 and giving the player the possibility to update their PC's ingame knowledge to what they might have already deducted behind the screen. Not every NPC might be suited to phrase them, though. Uncomment the according script blocks if you think your NPC should make conclusions.

Please note: 
The dialogues here are not listed in a tutorial, and most depend on the mod Road to Discovery (component 2: "Main Component: Tracking System") to be installed for them to trigger. 
They will flesh out the NPC and their reactions to the main story events *in addition* to what the SoD NPCs react to in general (i.e. all that is not covered by the tutorials). 
The listed dialogue possibilities are only suggestions. Use them, adapt them, or leave them out depending on your NPC's character and interests. Trying to use all of them might lead to your NPC sounding like a generic know-it-all. 

Dialogues are in "sod_important_reactions_dialogues.d". */

/* CONTENT */

/* 1. Hephernaan's own plans */
	/* 1.1 General reaction to Hephernaan is betraying Caelar: knowledge gained BEFORE the portal scene at Dragonspear basement; PC does NOT know that he is working for a fiend yet. */
	/* 1.2 General reaction to the knowledge that Hephernaan is working for a fiend: knowledge gained BEFORE the portal scene at Dragonspear basement */

/* 2. Caelar's ulterior motive is to save her uncle Aun. */
	/* 2.1 Conclude that Caelar's ulterior motive is to save her uncle: After knowing that she is planning on freeing souls from Avernus AND knowing that Aun went to Avernus for her. */
	/* 2.2 General reaction to the knowledge that Caelar's ulterior motive is to free her uncle BEFORE meeting Aun. */

/* 3. Detection: Found treatise "bdbook11": learn about the dormant portal and that godly blood is needed to open it - no specific dialogue attached to this one. */


/* 4. Incident at Boareskyr Bridge. */
	/* 4.1 Quick reaction line: will only show if The Boareskyr Bridge Scene mod is not installed nor didn't run. Uses the same line! -> crossmod with TBBS is in the tp2. */
	/* 4.2 General dialogue after bridge incident. Will run after timer. (True also in case The Boareskyr Bridge Scene mod is installed and the above quick reaction line run.) This follow-up dialogue triggers after timer is run. */

/* 5. Caelar wants to open a portal to Avernus. */
	/* 5.1 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND Hephernaan is working for a fiend. */
	/* 5.2 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND that there is a dormant rift below Dragonspear Castle. */
	/* 5.3 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND that PC's blood is needed to open portal below Dragonspear Castle. */
	/* 5.4 General reaction: Caelar wants to open a portal to Avernus. (No reaction to needed blood, PC might not know yet.) BEFORE portal scene at Dragonspear Castle */

/* 6. Caelar wants the PC for something */
	/* 6.1 Conclusion that Caelar didn't want to kill the PC but only kidnap after hearing about weak poison. */
	/* 6.2 Reaction after learning that Caelar needs the PC for something / wanted to kidnap the PC but does NOT know any specifics (BEFORE portal scene) */

/* 7. Caelar needs the PC's blood to open the portal. */
	/* 7.1 Conclusion that Caelar needs the PC's blood to open the portal: if Caelar needs Bhaalblood AND PC knows that she is no Bhaalspawn herself. */
	/* 7.2 Conclusion that Caelar needs the PC's blood to open the portal: if Caelar needs Bhaalblood AND wanted to kidnap the PC. */
	/* 7.3 General reaction to the knowledge that Caelar needs the PC's blood to open the portal (BEFORE portal betrayal scene): NPC already reacted to Caelar's plans to open a portal. */
	/* 7.4 General reaction to the knowledge that Caelar needs the PC's blood to open the portal (BEFORE portal betrayal scene): NPC did NOT yet react to Caelar's plans to open a portal. */

/* 8. Caelar is no child of Bhaal. */
	/* 8.1 Conclusion that Caelar is no child of Bhaal: after PC reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] AND hearing rumors that she is a child of Bhaal (or the portal would already be open / she wouldn't go for the PC.) */
	/* 8.2 Reaction to the knowledge that Caelar is no Bhaalchild. */

/* 9. Reaction to betrayal at the portal (only dialogue, no script blocks connected) */

/* 10. Final interjection after return from Avernus into Torsin de Lancie's dialogue (only dialogue, no script blocks connected). */

/* 11. Final campaign dialogue after returning from Avernus and crusade is defeated */



/* first we set a general dialogue timer. It could also be used to space timered friendship dialogues if Biff would have any. I'll use this here so the reaction dialogues do not all fire right after / at the incidents, which would be a bit annoying. - Timer length might need adjusting, haven't tried it ingame yet. 
You don't need this if you are already using a dialogue timer and want to use that instead. */
IF
	InParty("xxBiff")
	CombatCounter(0)
	!See([ENEMY])
	Global("xxBiff_InitDialTimer","LOCALS",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		SetGlobal("xxBiff_InitDialTimer","LOCALS",1)
END	

/* 1. Hephernaan's own plans */
/* GlobalLT("bd_plot","global",485) //before the portal is opened */

/* 1.1 General reaction to Hephernaan is betraying Caelar: knowledge gained BEFORE the portal scene at Dragonspear basement; PC does NOT know that he is working for a fiend yet. */
/* This will only play with Road to Discovery installed. */
IF
/* activation */
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!See("bdcaelar")
	!See("bdhepher")
	Global("C#RtD_HephernaanBetrayal","GLOBAL",2)
	Global("C#RtD_HephernaanFiend","GLOBAL",0)
	GlobalLT("bd_plot","global",485) //before portal scene
	Global("xxBiff_HephernaanBetrayal","GLOBAL",0) 
THEN 
	RESPONSE #100
		SetGlobal("xxBiff_HephernaanBetrayal","GLOBAL",1)
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_HephernaanBetrayal","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END

/* 1.2 General reaction to the knowledge that Hephernaan is working for a fiend */
/* this dialogue only happens if the knowledge is gained through triggers from Road to Discovery */
/* activation */
IF
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	OR(3) //not inside castle if crusaders still hold it
		!AreaCheck("bd4300")
		GlobalLT("bd_plot","GLOBAL",315)
		GlobalGT("bd_plot","GLOBAL",380)
	OR(2) //not inside the crusader camp near entrance 
		!AreaCheck("bd5100")
		!Range("TranBD5000",35) 
	!AreaCheck("bd5200")
	OR(2) //not inside the crusader camp near entrance 
		!AreaCheck("bd5000")
		!Range("TranBD5100",35) 
	Global("xxBiff_HephernaanFiend","GLOBAL",0) 
THEN 
	RESPONSE #100
		SetGlobal("xxBiff_HephernaanFiend","GLOBAL",1)
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_HephernaanFiend","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END


/* 2. Caelar's ulterior motive is to save her uncle Aun. */


/* Remove this line if you think your NPC should make this conclusion ##  

/* 2.1 Conclude that Caelar's ulterior motive is to save her uncle: After knowing that she is planning on freeing souls from Avernus and knowing that Aun went to Avernus for her. */
/* This dialogue makes only sense if it happens before the PC enters Avernus. Only other possibility considered to get information without Road to Discovery is via Themed Tweaks.*/
/* activation */
IF
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		GlobalGT("C#RtD_KnowsAunArgent","GLOBAL",3)
		Global("#L_SoDStat_DaustonTalk","GLOBAL",3) //from Themed Tweaks
	GlobalLT("C#RtD_CaelarPlan","GLOBAL",5)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_CaelarPlanAun","GLOBAL",0) 
	Global("C#SoD_CaelarPlanAun","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("xxBiff_CaelarPlanAun","GLOBAL",1) 
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_CaelarPlanAun","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END
#### Remove this line if you think your NPC should make this conclusion */

/* 2.2 General reaction to the knowledge that Caelar's ulterior motive is to free her uncle BEFORE meeting Aun. */
/* will only happen with Road to Discovery or if PC concluded it themselves */
/* activation */
IF
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("xxBiff_CaelarPlanAun","GLOBAL",2) //NPC concluded it 
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",4) //specualtes or knows it, from RtD
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_AllForAun","GLOBAL",0) 
THEN
	RESPONSE #100
		SetGlobal("xxBiff_AllForAun","GLOBAL",1) 
END
/* initiation */
/* same script block as below - same variable is used. */


/* 3. Detection: Found treatise "bdbook11": learn about the dormant portal and that godly blood is needed to open it. */
/* activate */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	PartyHasItem("bdbook11")
	Global("xxBiff_SoD_Foundbdbook11","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("xxBiff_SoD_Foundbdbook11","GLOBAL",1)
END




/* 4. Incident at Boareskyr Bridge. */

/* 4.1 Quick reaction line: will only show if The Boareskyr Bridge Scene mod is not installed / didn't run. Uses the same line! -> crossmod with TBBS is in the tp2. */
/* activation */
/* no timer here! */
IF
	InParty("xxBiff")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	!See([ENEMY])
	CombatCounter(0)
	GlobalGT("bd_plot","global",294) //after it happened
	GlobalLT("chapter","global",10) // not if game went on
	AreaCheck("bd2000")
	!InMyArea("bdireni")
	!InMyArea("bdfist24")
	Global("xxBiff_c#stff24_10","GLOBAL",0) // no interjection from tbbs
	Global("xxBiff_SoDBoareskyrBridgeScene","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("xxBiff_SoDBoareskyrBridgeScene","GLOBAL",1)
END

/* initiate */
IF
	InParty("xxBiff")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("xxBiff_SoDBoareskyrBridgeScene","GLOBAL",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600) //we set timer here so there is a space to whichever next dialogue
		StartDialogNoSet(Player1)
END

/* 4.2 General dialogue after bridge incident. Will run after timer. (True also in case The Boareskyr Bridge mod is installed and the above quick reaction line didn't run.) This follow-up dialogue triggers after timer is run. */
/* activation */
IF
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	GlobalGT("bd_plot","global",294) //after it happened
	OR(2)
		Global("xxBiff_SoDBoareskyrBridgeScene","GLOBAL",2) //quick reaction line run
		Global("xxBiff_c#stff24_10","GLOBAL",1) // interjection from tbbs run
	GlobalLT("xxBiff_SoDBoareskyrBridgeScene","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("xxBiff_SoDBoareskyrBridgeScene","GLOBAL",3)
END
/* initiate */
IF
	InParty("xxBiff")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("xxBiff_SoDBoareskyrBridgeScene","GLOBAL",3)
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END





/* 5. Caelar wants to open a portal to Avernus. */

/* Remove this line if you think your NPC should make this conclusion ##  

/* 5.1 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND Hephernaan is working for a fiend. */
/* only with Road to Discovery */
/* activation */
IF 
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#RtD_CaelarPlan","GLOBAL",3)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
	GlobalLT("C#RtD_CaelarPlan","GLOBAL",4)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_SoD_PortalConclusion","GLOBAL",0)
	Global("C#SoD_PortalConclusion_1","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("xxBiff_SoD_PortalConclusion","GLOBAL",1) 
END
/* initiation block see below! */

/* 5.2 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND that there is a dormant rift below Dragonspear Castle. */
/* activation */
IF 
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#RtD_RoadToDiscovery","GLOBAL",0)
	OR(2)
		GlobalGT("bd_plot","global",169) //after CWC Parley with Caelar
		Global("C#RtD_RoadToDiscovery","GLOBAL",1)
	OR(3)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",1)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2)
		Global("xxBiff_SoD_Foundbdbook11","GLOBAL",1)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_SoD_PortalConclusion","GLOBAL",0)
	Global("C#SoD_PortalConclusion_2","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("xxBiff_SoD_PortalConclusion","GLOBAL",2) 
END
/* initiation block see below! */

/* 5.3 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND that PC's blood is needed to open portal below Dragonspear Castle. */
/* triggers with Road to Discovery or if NPC conlcuded that Caelar needs PC's blood for portal */
/* activation */
IF 
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#RtD_CaelarPlan","GLOBAL",3)
	OR(2)
		GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2)
		Global("xxBiff_PortalPCBloodConclusion","GLOBAL",3)
	GlobalLT("C#RtD_CaelarPlan","GLOBAL",4)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_SoD_PortalConclusion","GLOBAL",0)
	Global("C#SoD_PortalConclusion_3","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("xxBiff_SoD_PortalConclusion","GLOBAL",3) 
END

/* Initiation for all three conclusion possibilities. Appropriate dialogue will be triggered via variable value set in the activation block. */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("xxBiff_SoD_PortalConclusion","GLOBAL",0)
	GlobalLT("xxBiff_SoD_PortalConclusion","GLOBAL",4)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END
#### Remove this line if you think your NPC should make this conclusion */

/* 5.4 General reaction: Caelar wants to open a portal to Avernus. (No reaction to needed blood, PC might not know yet.) BEFORE portal scene at Dragonspear Castle */
/* without Road to Discovery, this will trigger after parley at Dead Man's Pass or if NPC had conclusion himself. */
/* activation */
IF
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	!See([ENEMY])
	See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",2) //from Road to Discovery
	OR(4)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3) //from Road to Discovery
		Global("xxBiff_SoD_PortalConclusion","GLOBAL",4)
  		GlobalGT("bd_plot","global",390) //after meeting with Caelar on Dead Man's Pass
		Global("C#RtD_RoadToDiscovery","GLOBAL",1) //check for Road to Discovery
	OR(3)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3) //from Road to Discovery
		Global("xxBiff_SoD_PortalConclusion","GLOBAL",4)
		Global("C#RtD_RoadToDiscovery","GLOBAL",0) //broaden trigger if Road to Discovery is not installed
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_PortalAvernus","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("xxBiff_PortalAvernus","GLOBAL",1)
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_PortalAvernus","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END

/* 6. Caelar wants the PC for something */

/* Remove this line if you think your NPC should make this conclusion ##  

/* 6.1 Conclusion that Caelar didn't want to kill the PC but only kidnap after hearing about weak poison. */
/* will only trigger with Road to Discovery */
/* activation */
IF 
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#RtD_WeakPoison","GLOBAL",1)
	Global("C#RtD_CaelarWantsPC","GLOBAL",0)
	Global("C#RtD_CaelarKidnap","GLOBAL",0)
	Global("#L_SoDStat_WeakPoison","GLOBAL",0)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_SoDKidnapConclusion","GLOBAL",0)
	Global("C#SoD_KidnapConclusion","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("xxBiff_SoDKidnapConclusion","GLOBAL",1)
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_SoDKidnapConclusion","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END
#### Remove this line if you think your NPC should make this conclusion */

/* 6.2 Reaction after learning that Caelar needs the PC / PC's blood for something but does NOT know any specifics (BEFORE portal scene) */
/* this only fires with Road to Discovery or if NPC concluded the kidnapping */
/* activation */
IF
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!See("bdcaelar")
	!See("bdhepher")
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",3)
	GlobalLT("C#RtD_WantBhaalBlood","GLOBAL",2)
	OR(3)
		GlobalGT("C#RtD_CaelarWantsPC","GLOBAL",0)
		Global("C#RtD_WantBhaalBlood","GLOBAL",1)
		Global("xxBiff_SoDKidnapConclusion","GLOBAL",2)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_SoDCaelarWantsPC","GLOBAL",0)
THEN 
	RESPONSE #100
		SetGlobal("xxBiff_SoDCaelarWantsPC","GLOBAL",1)
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_SoDCaelarWantsPC","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END



/* 7. Caelar needs the PC's blood to open the portal. */

/* Remove this line if you think your NPC should make this conclusion ##  

/* 7.1 Conclusion that Caelar needs the PC's blood to open the portal: if Caelar needs Bhaalblood to open portal AND PC knows that she is no Bhaalspawn herself. */
/* this can trigger without Road to Discovery if treatise was found and NPC concluded that Caelar wants to open a portal. */
IF 
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2) 
		Global("xxBiff_SoD_Foundbdbook11","GLOBAL",1)
	OR(2)
		GlobalGT("C#RtD_CaelarBhaalChild","GLOBAL",1)
		GlobalGT("bd_mdd018","global",0)
	OR(2)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#RtD_RoadToDiscovery","GLOBAL",0)
	OR(3)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3)
		Global("xxBiff_SoD_PortalConclusion","GLOBAL",4)
		Global("C#RtD_RoadToDiscovery","GLOBAL",1)
	GlobalLT("C#RtD_WantBhaalBlood","GLOBAL",2)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",3)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_PortalPCBloodConclusion","GLOBAL",0)
	Global("C#SoD_PortalPCBloodConclusion_1","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("xxBiff_PortalPCBloodConclusion","GLOBAL",1)
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_PortalPCBloodConclusion","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END

#### Remove this line if you think your NPC should make this conclusion */


/* Remove this line if you think your NPC should make this conclusion ##  

/* 7.2 Conclusion that Caelar needs the PC's blood to open the portal: if Caelar needs Bhaalblood AND wanted to kidnap the PC. */
/* will only trigger with Road to Discovery variables. */
/* activation */
IF 
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2) 
		Global("xxBiff_SoD_Foundbdbook11","GLOBAL",1)
	OR(3)
		Global("C#RtD_WantBhaalBlood","GLOBAL",1)
		Global("C#RtD_CaelarKidnap","GLOBAL",1)
		GlobalGT("C#RtD_CaelarWantsPC","GLOBAL",1)
	OR(2)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3)
		Global("xxBiff_SoD_PortalConclusion","GLOBAL",4)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",3)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_PortalPCBloodConclusion","GLOBAL",0)
	Global("C#SoD_PortalPCBloodConclusion_2","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("xxBiff_PortalPCBloodConclusion","GLOBAL",2)
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_PortalPCBloodConclusion","GLOBAL",2)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END
#### Remove this line if you think your NPC should make this conclusion */

/* 7.3 + 7.4 General reaction to the knowledge that Caelar needs the PC's blood to open the portal. */
/* activation */
IF
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	!See([ENEMY])
	See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	OR(2)
		GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2) //from Road to Discovery
		Global("xxBiff_PortalPCBloodConclusion","GLOBAL",3)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_PortalPCBlood","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("xxBiff_PortalPCBlood","GLOBAL",1)
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_PortalPCBlood","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END




/* 8. Caelar is no child of Bhaal. */

/* Remove this line if you think your NPC should make this conclusion ##  

/* 8.1 Conclusion that Caelar is no child of Bhaal: after PC reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] AND hearing rumors that she is a child of Bhaal (or the portal would already be open / she wouldn't go for the PC.) */
/* will only trigger with Road to Discovery because there is no other ingame trigger to check whether the PC heard the rumor. */
/* activation */
IF
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2)
	Global("C#RtD_CaelarBhaalChild","GLOBAL",1)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("xxBiff_CaelarNoBhaalChild","GLOBAL",0) 
	Global("C#SoD_CaelarNoBhaalChild","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("xxBiff_CaelarNoBhaalChild","GLOBAL",1) 
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_CaelarNoBhaalChild","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END
#### Remove this line if you think your NPC should make this conclusion */

/* 8.2 Reaction to the knowledge that Caelar is no Bhaalchild. */
/* activation */
/* without Road to Discovery, this will only trigger if Caelar was asked about it in the Coastway Crossing parley. */
IF
	InParty("xxBiff")
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(3)
  		GlobalGT("bd_mdd018","global",0) //Caelar told she is no child of Bhaal
		GlobalGT("C#RtD_CaelarBhaalChild","GLOBAL",1) //from Road to Discovery
		Global("xxBiff_CaelarNoBhaalChild","GLOBAL",2)
	GlobalLT("xxBiff_CaelarNoBhaalChild","GLOBAL",3) 
THEN
	RESPONSE #100
		SetGlobal("xxBiff_CaelarNoBhaalChild","GLOBAL",3) 
END
/* initiation */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_CaelarNoBhaalChild","GLOBAL",3)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END


/* 11. Final campaign dialogue after returning from Avernus and crusade is defeated */

/* activation */

IF
	InParty("xxBiff")
	OR(2)
		!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
		See("BDDAZZO")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("bd_plot","global",586)
	AreaCheck("bd4300")
	Global("xxBiff_SoDEnd","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("xxBiff_SoDEnd","GLOBAL",1)
END

/* trigger */
IF
	InParty("xxBiff")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("xxBiff_SoDEnd","GLOBAL",1)	
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

