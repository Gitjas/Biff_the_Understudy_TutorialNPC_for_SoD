/* (T2) bd4100_patch.baf. CHARNAME is accused of murder. Biff comments */

/* first script block: Biff makes a comment in case of lower reputation */
IF
	Global("bd_plot","global",591)
	!GlobalTimerNotExpired("bd_mdd1352_ot_timer","bd4100") //this is SoD's own timer and makes sure all comments fire before the next scripted event kicks in 
	Global("xxBiff_SoDMurderAccusations","bd4100",0)
	ReputationLT(Player1,10) //checks for low reputation. Replace this with whatever you want the NPC's reaction to be dependent on 
	IsValidForPartyDialogue("xxBiff")  
THEN
//	RESPONSE #100 //We want Biff to comment always, so we leave these two lines out. Take these in if you want a 50% chance of your NPC not commenting on the murder accusations. 
//		SetGlobal("xxBiff_SoDMurderAccusations","bd4100",1)
	RESPONSE #100
		SetGlobal("xxBiff_SoDMurderAccusations","bd4100",1)
		SetGlobalTimer("bd_mdd1352_ot_timer","bd4100",ONE_ROUND) //set the timer according to how long the comment is. ONE_ROUND equals ONE_MINUTE = 6 seconds realtime. You can put in the amount of realtime seconds as a number directly.
		DisplayStringHead("xxBiff",~Well, <CHARNAME> might have not been the friendliest, but I know the script so... Nope, I don't think <PRO_HESHE> did it.~)  
		Continue()
END

/* second script block: Biff makes a comment in case of higher reputation */
IF
	Global("bd_plot","global",591)
	!GlobalTimerNotExpired("bd_mdd1352_ot_timer","bd4100")
	Global("xxBiff_SoDMurderAccusations","bd4100",0)
	!ReputationLT(Player1,10) //checks for high reputation. If you used something else above, here goes the direct opposite of the trigger you used above.
	IsValidForPartyDialogue("xxBiff")  
THEN
//	RESPONSE #100 //uncomment these 2 lines for the original SoD 50% chance
//		SetGlobal("xxBiff_SoDMurderAccusations","bd4100",1)
	RESPONSE #100
		SetGlobal("xxBiff_SoDMurderAccusations","bd4100",1)
		SetGlobalTimer("bd_mdd1352_ot_timer","bd4100",ONE_ROUND)
		DisplayStringHead("xxBiff",~Whoa, <CHARNAME>'s such a nice fellow. (And I know the script so... Nope, I don't think <PRO_HESHE> did it.)~)   
		Continue()
END