/*

Comments:
(T1) - content of tutorial "Part 1: Automatic Transition of NPCs into SoD and between Camps in SoD"
(T2) - content of tutorial "Part 2: Make Your NPC Comment and Move Along at the End of SoD"
(T4) - content of tutorial "Part 4: SoD Banter System for Your NPC"


*/

BACKUP ~Biffsodtutorial/backup~ //put the mod folder name here, for the rest of the mod, you can use %MOD_FOLDER% (decreases the amount of copy&paste errors...)

AUTHOR ~https://www.gibberlings3.net/forums/topic/30139-jasteys-sod-npc-modding-tutorials/~ //The info given here will be shown if the install fails with the note to send the DEBUG here for helfile p. Put here wherever you want the player to seek your help in case the installation fails. I used to give my email address but switched to giving a forum link as it has several advantages.

VERSION ~5~ //version number. Always give a specific version number to any mod version you make public, or debugging a player's install will be difficult.

README ~%MOD_FOLDER%/readme.Biff.%LANGUAGE%.txt~ //The file specified here will be called after the language call, but before the installation of the mod. Can also be html.

AUTO_TRA ~%MOD_FOLDER%/tra/%s~ /* This is where the installer looks for text strings, the "%s" is for the chosen language version. 
Please note: text files for the EE need to be in utf8 without BOM. However, in this tutorial mod the tra files are in ANSI. This is because so far, the usual way of handling tra files in mod installation was direct conversion from ANSI to utf8 using HANDLE_CHARSETS with iconv.exe. This is the way how it is done here. */
/* please note: the tras are called in the order: 
LANGUAGE, WITH_TRA, USING and AUTO_TRA
with the last one overwriting the first, meaning: you can overwrite strings called by LANGUAGE with WITH_TRA, both will be overwritten by USING, but if there is a tra file of the same name of the file you are compiling in the AUTO_TRA folder, the strings with matching numbers from there will be taken with highest priority. */

ALWAYS //put stuff that should be done by every install of every component here. 

/* this is needed if ingame text ressources need to be patched e.g. inside a script so the text matches 100%: needed for exact match in REPLACE_TEXTUALLY */
  INCLUDE ~%MOD_FOLDER%/lib/extra_regexp_vars.tph~
  OUTER_SPRINT newline ~%WNL%%LNL%%MNL%%TAB% ~

/* (T3) Define a variable to account for the differences in string ref numbers between BG:SoD and EET. - Works for string references in d- or baf-files. */
  ACTION_IF GAME_IS ~bgee~ BEGIN
    OUTER_SPRINT ~2~ ~~
  END

  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SPRINT ~2~ ~2~
  END

/* (T1) Definition of variables to distinguish between SoD and BG1 (for banters or script blocks) */
  ACTION_IF (GAME_IS ~bgee~) THEN BEGIN
    ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~14~) BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~GlobalLT("BD_PLOT","GLOBAL",41)~
      OUTER_SPRINT ~IT_IS_SOD~ ~GlobalGT("bd_plot","global",40)~
    END ELSE ACTION_IF (MOD_IS_INSTALLED ~transitions.tp2~ ~0~) BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~GlobalLT("BD_PLOT","GLOBAL",41)~
      OUTER_SPRINT ~IT_IS_SOD~ ~GlobalGT("bd_plot","global",40)~
    END ELSE BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~Global("BD_PLOT","GLOBAL",0)~
      OUTER_SPRINT ~IT_IS_SOD~ ~GlobalGT("bd_plot","global",0)~
    END
  END
  ACTION_IF GAME_IS ~eet~ THEN BEGIN
    ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~14~) BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~OR(2) Global("ENDOFBG1","GLOBAL",0) GlobalLT("BD_PLOT","GLOBAL",41)~
      OUTER_SPRINT ~IT_IS_SOD~ ~Global("ENDOFBG1","GLOBAL",1) GlobalGT("bd_plot","global",40)~
    END ELSE ACTION_IF (MOD_IS_INSTALLED ~transitions.tp2~ ~0~) BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~OR(2) Global("ENDOFBG1","GLOBAL",0) GlobalLT("BD_PLOT","GLOBAL",41)~
      OUTER_SPRINT ~IT_IS_SOD~ ~Global("ENDOFBG1","GLOBAL",1) GlobalGT("bd_plot","global",40)~
    END ELSE BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~Global("ENDOFBG1","GLOBAL",0)~
      OUTER_SPRINT ~IT_IS_SOD~ ~Global("ENDOFBG1","GLOBAL",1)~
    END
  END

  ACTION_DEFINE_ARRAY traxxreload BEGIN game END //game.tra contains ingame strings so it needs to be reloaded after the utf-8 conversion
//  ACTION_DEFINE_ARRAY traxxnoconvert BEGIN END //if you have a tra file that doesn't need to be reloaded (e.g. a pure setup.tra for the install process only) or that is already in utf-8 format and shouldn't be included for conversion, you could specify it here

  LAF HANDLE_CHARSETS
	INT_VAR
		infer_charsets = 1 //choses the correct encoding for different languages
	STR_VAR
		tra_path = EVAL ~%MOD_FOLDER%/tra~ //path to the LANGUAGE folders
//		noconvert_array = traxxnoconvert //if you specified the array above
		reload_array = traxxreload //loads the array defined above
		iconv_path = EVAL ~%MOD_FOLDER%/tra/iconv~ //path to the inconv folder
  END

END //ALWAYS

/* language choices the player gets to chose from upon install. The tra files needed for the tp2 need to be read in here. Note: any ingame texts need to be reloaded after the conversion to utf8 as it is done above. */
LANGUAGE ~English~ ~english~ ~%MOD_FOLDER%/tra/english/setup.tra~ ~%MOD_FOLDER%/tra/english/game.tra~

/* commented out as it's just an example. Ingame texts are not translated.
LANGUAGE ~German~ ~german~ ~%MOD_FOLDER%/tra/german/setup.tra~ 
~%MOD_FOLDER%/tra/german/game.tra~
*/


///////////////////////////////////////////////////////////
// Main Component //
/////////////////////////////////////////////////////////

BEGIN @90000 /* ~Biff the Understudy: joinable "tutorial" NPC for SoD~ */ 
DESIGNATED 0 /* giving the component a DESIGNATED number which will not change in the future is of great importance for crossmod compatibility. In this case, it would have been the "0th" component anyway, but especially for other components (if your mod has them), this would be important. */
LABEL xxbiff_sod_tutorial_mod-main /* define a unique global label for this mod component(!). It basically does what DESIGNATED does, only better. And it helps Project Infinity remember the component when setting up an install list. */

/* make sure the mod cannot be installed to a wrong game, like BGII etc. */
/* Biff can only be installed to BG:SoD and EET. */
REQUIRE_PREDICATE (GAME_IS ~bgee eet~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) @90001 /* ~This mod is really only for SoD~ */

/* Portraits - Biff doesn't have any. Add portraits with the naming scheme used for the cre patching below for your mod */
COPY ~%MOD_FOLDER%/Portraits~ ~override~

/* Biff's cre file */
/* this tutorial is not a good example for how to create your cre-file etc. I just took the exsiting Biff the Understudy (DIALOGMO.CRE) and adjusted his Allegiance inside the cre file, his scriptname (death variable), his override script and his greetings dialogue.  */

COPY ~%MOD_FOLDER%/cre/xxbiff.cre~ ~override~
SAY NAME1 #11662 //we don't need this for Biff as his name is in the dialog.tlk already
SAY NAME2 #11662
WRITE_EVALUATED_ASCII 0x280 ~xxBiff~ #32 // script name / DV
WRITE_EVALUATED_ASCII 0x2cc ~xxBiffsG~ #8 //greetings dialogue
WRITE_EVALUATED_ASCII 0x248 ~xxBiffs~ #8   // Override script
WRITE_EVALUATED_ASCII 0x250 ~~ #8   // Class script
WRITE_EVALUATED_ASCII 0x258 ~~ #8   // Race script
WRITE_EVALUATED_ASCII 0x260 ~~ #8   // General script
WRITE_EVALUATED_ASCII 0x268 ~~ #8   // Default script
WRITE_EVALUATED_ASCII 0X34 ~xxBiffpm~ #8 //medium portrait
WRITE_EVALUATED_ASCII 0X3c ~xxBiffpl~ #8 //small portrait
/* Biff doesn't have a sound set (yet) */
SAY INITIAL_MEETING ~~
SAY MORALE ~~   
SAY HAPPY ~~        
SAY UNHAPPY_ANNOYED ~~   
SAY UNHAPPY_SERIOUS ~~   
SAY UNHAPPY_BREAKING ~~   
SAY LEADER ~~   
SAY TIRED ~~   
SAY BORED ~~ 
SAY BATTLE_CRY1 ~~  
SAY BATTLE_CRY2 ~~   
SAY BATTLE_CRY3 ~~   
SAY BATTLE_CRY4~~   
SAY DAMAGE ~~
SAY DYING ~~
SAY HURT ~~   
SAY AREA_FOREST ~~   
SAY AREA_CITY ~~  
SAY AREA_DUNGEON ~~   
SAY SELECT_COMMON1 ~~   
SAY SELECT_COMMON2 ~~   
SAY SELECT_COMMON3 ~~   
SAY SELECT_COMMON4 ~~   
SAY SELECT_COMMON5 ~~   
SAY SELECT_COMMON6 ~~   
SAY SELECT_ACTION1 ~~   
SAY SELECT_ACTION2 ~~   
SAY SELECT_ACTION3 ~~   
SAY SELECT_ACTION4 ~~   
SAY SELECT_ACTION5 ~~   
SAY SELECT_ACTION6 ~~   
SAY SELECT_ACTION7 ~~   
SAY SELECT_RARE1 ~~   
SAY SELECT_RARE2  ~~   
SAY CRITICAL_HIT ~~   
SAY CRITICAL_MISS ~~   
SAY TARGET_IMMUNE ~~   
SAY INVENTORY_FULL ~~ 
SAY BIO ~~


/* Biff's dialogues */

/* (T1) SoD greeting dialogue */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogue/greetingsdialogue.d~

//actually, we do not really need EVALUATE_BUFFER as long as the file doesn't use any OUTER_SPRINT variables etc. but it doesn't hurt, either.

/* (T1) SoD kickout dialogue */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogue/kickoutdialogue.d~

/* (T2/T3)) Biff's comments on SoD events */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogue/sod_event_comments.d~

/* (T?)) Biff's reactions to important knowledge about Caelar's plans. Works best with Road to Discovery installed. */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogue/sod_important_reactions_dialogues.d~


/* script compilation */

// (T4) Biff's SoD script xxBiffs.baf 
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/baf/xxBiffs.baf~ //creates Biff's OVERRIDE script "xxBiffs.bcs" from the file name.

/* (T3) patching general script additions to Biff's now existing OVERRIDE script: */

/* (T?)) Biff's reactions to important knowledge about Caelar's plans. Works best with Road to Discovery installed. Compile this as early as possible so the game relevant reactions happen with higher priority. */
/* content of "sod_important_reactions_scripts.baf" will be added to "xxBiffs.bcs". */
EXTEND_BOTTOM ~xxBiffs.bcs~ ~%MOD_FOLDER%/baf/sod_important_reactions_scripts.baf~
  EVALUATE_BUFFER	

/* content of "general_script_additions.baf" will also be added to "xxBiffs.bcs". */
EXTEND_BOTTOM ~xxBiffs.bcs~ ~%MOD_FOLDER%/baf/general_script_additions.baf~
  EVALUATE_BUFFER	

/* empty Timer setting. Needs to be last after any script blocks to Biff's OVERRIDE script so it doesn't block any timered dialogues! */
/* add general resetting of the dialogue space timer (timer so dialogues and comments do not fire directly after each other) */
/* this needs to be after any script blocks that use this timer */

<<<<<<<< ...inlined/xxBiffs_timer.baf
/* Let's reset the timer once it run out without a valid dialogue, so the next reaction dialogue will not trigger right after the incident. */
/* make sure this is at the END of all script blocks using this timer or any script block using this timer elsewhere will never trigger! */
IF
	InParty("xxBiff")
	CombatCounter(0)
	!See([ENEMY])
	ActionListEmpty()
	!RealGlobalTimerNotExpired("xxBiff_DialogReactionTimer","GLOBAL")
THEN
	RESPONSE #100
		RealSetGlobalTimer("xxBiff_DialogReactionTimer","GLOBAL",600)
END	
>>>>>>>>
EXTEND_BOTTOM ~xxBiffs.bcs~ ~...inlined/xxBiffs_timer.baf~

/* (T4) banter scripts with Corwin (and Minsc) */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/baf/xxBiff01.baf~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/baf/xxBiff02.baf~


/* script compilation: modify existing game scripts. In alphabetical order with regard to ingame scripts. */

/* (T1) let Biff (re)appear in front of the Duchal Palace */
EXTEND_BOTTOM ~bd0010.bcs~ ~%MOD_FOLDER%/baf/bd0010_patch.baf~ EVALUATE_BUFFER

/* (T3) Comment in random area (Canyon Ambush) bd0063.are */
EXTEND_BOTTOM ~bd0063.bcs~ ~%MOD_FOLDER%/baf/bd0063_commenting.baf~
EVALUATE_BUFFER

/* (T3) Waiting to march out of BG city: make Biff comment while waiting to leave */
EXTEND_TOP ~bd0101.bcs~ ~%MOD_FOLDER%/baf/bd0101_commenting_et.baf~
  EVALUATE_BUFFER

/* (T1) Biff is in front of Ducal Palace if told to wait - or Player marches onto the battle without recruiting NPCs in Baldur' Gate: Biff will be in front of the palace, too */
EXTEND_BOTTOM ~bd0101.bcs~ ~%MOD_FOLDER%/baf/bd0101_patch.baf~
  EVALUATE_BUFFER

/* (T3) In case Biff is in party the last night the PC slept in Ducal Palace - comment after Skie is gone */
EXTEND_TOP ~bd0103.bcs~   ~%MOD_FOLDER%/baf/bd0103_commenting_et.baf~  
  EVALUATE_BUFFER

/* (T1) after Korlasz is defeated: PC is moved to Ducal Palace. Joined NPCs leave */
/* +++ Biff doesn't really need this since he won't be available before the transition to the city. This is for demonstration purposes if your NPC carries over from BG:EE +++ */
EXTEND_TOP ~bd0103.bcs~ ~%MOD_FOLDER%/baf/bd0103_patch.baf~
  EVALUATE_BUFFER 

/* (T2) Biff visits PC in Prison */
EXTEND_TOP ~bd0104.bcs~ ~%MOD_FOLDER%/baf/bd0104_patch.baf~
  EVALUATE_BUFFER

/* (T3) Comment after killing the blind Wyrmlings in Cave (bd0113.bcs) */
EXTEND_BOTTOM ~bd0113.bcs~ ~%MOD_FOLDER%/baf/bd0113_commenting.baf~
EVALUATE_BUFFER

/* (T3) Comment after killing the Rhinoceros Beetle in bd0114.are */
EXTEND_BOTTOM ~bd0114.bcs~ ~%MOD_FOLDER%/baf/bd0114_commenting.baf~
EVALUATE_BUFFER

/* (T3) comment after defeating the first enemies in Korlasz' Crypt */
EXTEND_TOP ~bd0120.bcs~ ~%MOD_FOLDER%/baf/bd0120_commenting_et.baf~
  EVALUATE_BUFFER

/* (T3) after Korlasz is defeated: DisplayStringHead of his further plans. 
Same script blocks get patched to both area scripts of Korlasz' Crypt */
EXTEND_BOTTOM ~bd0120.bcs~ ~%MOD_FOLDER%/baf/bd0120_commenting.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~bd0130.bcs~ ~%MOD_FOLDER%/baf/bd0120_commenting.baf~
  EVALUATE_BUFFER

/* (T1) Biff moves to war camp in bd1000.are */
EXTEND_TOP ~bd1000.bcs~ ~%MOD_FOLDER%/baf/bd1000_patch.baf~
  EVALUATE_BUFFER

/* (T3) Reaction to Caelar's first appearance on bridge */
EXTEND_BOTTOM ~bd1000.bcs~ ~%MOD_FOLDER%/baf/bd1000_commenting.baf~
  EVALUATE_BUFFER

/* (T1) Patch your NPC's "Move Camp" variable to the original game's script block (bd1000.bcs) */
COPY_EXISTING ~bd1000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(SetGlobal("bd_move_npcs","bd1000",1)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("xxBiff_MoveCamp","MYAREA",1)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T3) Battle at Bridgefort starts */
EXTEND_BOTTOM ~bd2000.bcs~ ~%MOD_FOLDER%/baf/bd2000_commenting.baf~
  EVALUATE_BUFFER

/* (T3) PC gave Bridgefort Castle to the crusaders */
EXTEND_TOP ~bd2000.bcs~ ~%MOD_FOLDER%/baf/bd2000_commenting_et.baf~
  EVALUATE_BUFFER

/* (T1) Last camp is in bd3000.are. In case Biff was never in party or kicked out to wait somewhere else, he will also be back in the (new) camp. */
EXTEND_TOP ~bd3000.bcs~ ~%MOD_FOLDER%/baf/bd3000_patch.baf~
  EVALUATE_BUFFER

/* (T1) Patch your NPC's "Move Camp" variable to the original game's script block (bd3000.bcs) */
COPY_EXISTING ~bd3000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(SetGlobal("bd_move_npcs","bd3000",1)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("xxBiff_MoveCamp","MYAREA",1)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T1) Patch your NPC's "Move Camp" variable to the original game's script block (bd3000.bcs)  to call back NPC when battle starts */
COPY_EXISTING ~bd3000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(SetGlobal("bd_move_voghiln","bd3000",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("xxBiff_MoveCamp","MYAREA",0)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T2) Biff comments on murder accusation */
EXTEND_TOP ~bd4100.bcs~ ~%MOD_FOLDER%/baf/bd4100_patch.baf~
  EVALUATE_BUFFER

/* (T3) Comments in bd4300.are (Bridgefort Castle interior and Avernus portal) */
EXTEND_TOP ~bd4300.bcs~ ~%MOD_FOLDER%/baf/bd4300_commenting_et.baf~
EVALUATE_BUFFER	

/* (T3) Comment upon entering Avernus */
EXTEND_BOTTOM ~bd4400.bcs~ ~%MOD_FOLDER%/baf/bd4400_commenting.baf~
EVALUATE_BUFFER

/* (T3) Comment in the Avernus Elevator */
EXTEND_TOP ~bd4601.bcs~ ~%MOD_FOLDER%/baf/bd4601_commenting_et.baf~
EVALUATE_BUFFER

/* (T3) Comment before entering the Cave with the blind Wyrmlings (bd5100aw.bcs) */
EXTEND_BOTTOM ~bd5100aw.bcs~ ~%MOD_FOLDER%/baf/bd5100aw_commenting.baf~
EVALUATE_BUFFER

/* (T3) Comment when battle starts in Kanaglym (bd5300.are, Underground River) */
EXTEND_BOTTOM ~bd5300.bcs~ ~%MOD_FOLDER%/baf/bd5300_commenting.baf~
EVALUATE_BUFFER

/* [DEPRECATED] - refer to Tutorial 3 "Add Other General SoD Behavior to Your NPC: XP Adjustment, Actions in Story Cutscenes, Commenting of Main Story Events" 
/* (T3) Last area bd6100.are: comment in abduction scene (for EET or SoD with Jastey's SoD Tweak Pack) */
EXTEND_TOP ~bd6100.bcs~ ~%MOD_FOLDER%/baf/bd6100_commenting_et.baf~
EVALUATE_BUFFER
*/

/* (T2) Biff waits in front of the prison exit */
EXTEND_TOP ~bd6200.bcs~ ~%MOD_FOLDER%/baf/bd6200_patch.baf~
  EVALUATE_BUFFER

/* (T1) and the same for the next camp in bd7100.are: */
EXTEND_BOTTOM ~bd7100.bcs~ ~%MOD_FOLDER%/baf/bd7100_patch.baf~
  EVALUATE_BUFFER

/* (T1) Patch your NPC's "Move Camp" variable to the original game's script block (bd7100.bcs) */
COPY_EXISTING ~bd7100.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(SetGlobal("bd_move_npcs","bd7100",1)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("xxBiff_MoveCamp","MYAREA",1)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T1) Move NPC to entrance of the Allied Siege Camp (Crusade battle) */
EXTEND_BOTTOM ~bdasc3.bcs~ ~%MOD_FOLDER%/baf/bdasc3_patch.baf~
  EVALUATE_BUFFER

/* (T3) Comment in Dragonspear Castle Exterior */
EXTEND_BOTTOM ~bdbark01.bcs~ ~%MOD_FOLDER%/baf/bdbark01_commenting.baf~
EVALUATE_BUFFER

/* (T3) Move the PC in the cutscene of the plotting nobles in Three Old Kegs (bdc116d.bcs) */
EXTEND_TOP ~bdc116d.bcs~ ~%MOD_FOLDER%/baf/bdc116d_patch.baf~
EVALUATE_BUFFER

/* (T4) 2nd banter is initiated in CORWIN's script by setting the area script to the custom script containing our banter. */
EXTEND_BOTTOM ~bdcorwin.bcs~ ~%MOD_FOLDER%/baf/banter_corwin.baf~
EVALUATE_BUFFER

/*  (T3) March out of BG city: make Biff move when the coalition start marching out of BG city */
EXTEND_TOP ~bdcut08.bcs~ ~%MOD_FOLDER%/baf/bdcut08_patch.baf~
  EVALUATE_BUFFER

/* (T1) Biff is kicked in hell dimension - teleport to last hell area bd4400.are */
EXTEND_TOP ~bdcut58.bcs~ ~%MOD_FOLDER%/baf/bdcut58_patch.baf~
  EVALUATE_BUFFER

/* (T1) Biff is kicked in hell dimension or was waiting elsewhere- teleport to Dragonspear Castle interior (bd4300.are) after PC returns from Avernus */
EXTEND_TOP ~bdcut59b.bcs~ ~%MOD_FOLDER%/baf/bdcut59b_patch.baf~
  EVALUATE_BUFFER

/* (T2) after PC gets arrested for murder: Biff leaves party */
EXTEND_TOP ~bdcut61.bcs~ ~%MOD_FOLDER%/baf/bdcut61_patch.baf~
  EVALUATE_BUFFER

/* [DEPRECATED] - refer to Tutorial 2 "Make Your NPC Comment and Move Along at the End of SoD"
/* (T2) Let Biff join after PC escaped from prison (if there is still a free slot in the group) */
EXTEND_TOP ~bdcut64x.bcs~ ~%MOD_FOLDER%/baf/bdcut64x_patch.baf~
  EVALUATE_BUFFER
*/

/* [DEPRECATED] - refer to Tutorial 2 "Make Your NPC Comment and Move Along at the End of SoD"
/* (T2) Biff should move with the group in the last area (abduction scene, bd6100.are) */
COPY_EXISTING ~bdcut65.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN

		SPRINT textToReplace ~\(SetAreaScript("",OVERRIDE)\)[%newline%]*\(END\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 
\2 

IF
	Global("xxBiff_SoDEndMove","bd6100",0) 
	InMyArea("xxBiff")
	!Dead("xxBiff")  
	!InParty("xxBiff")  
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SetGlobal("xxBiff_SoDEndMove","bd6100",1) 
		ActionOverride("xxBiff",MoveToPoint([930.795])) //you can leave these coordinates for your NPC, too
END
~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY_IF_IT_CHANGES
*/


/* (T3) React in case crusaders in cages are killed by spikes (bd7230.are) */
/* Patch bdlever2.bcs */
COPY_EXISTING ~bdlever2.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Kill("bdkharmy")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("xxBiff_UsedSpikes","GLOBAL",1) \1~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride(Player1,RemovePaladinHood())\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("xxBiff_SpikeTrapFall","GLOBAL",1) \1~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride(Player1,RemoveRangerHood())\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("xxBiff_SpikeTrapFall","GLOBAL",1) \1~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T1) return to camp if kicked out */
EXTEND_TOP ~bdparty.bcs~ ~%MOD_FOLDER%/baf/bdparty_patch.baf~
  EVALUATE_BUFFER

/* (T3) Comment on closed vault door (to portal) in Dragonspear Castle Interior */
EXTEND_BOTTOM ~bdvaultd.bcs~ ~%MOD_FOLDER%/baf/bdvaultd_patch.baf~
EVALUATE_BUFFER


/* bddialog.2da patching */
/* by patching the bddialog.2da the engine knows which dialogues to call in SoD for Biff for joined and kickout dialogue and the dream script (triggers upon resting) */

/* add Biff's dialogues and dream script to bddialog.2da */
APPEND ~bddialog.2da~
~xxBiff xxBiffsP xxBiffJ xxBiffD~ //first entry if Biff's death variable
UNLESS ~xxBiff~ //Biff's death variable

/* (T1) EET compatibility: Biff is an BG1 (SoD) NPC only. His name will be mentioned on EET's fate spirit in ToB, but he will not be summonable. */

ACTION_IF GAME_IS ~eet~ BEGIN
  INCLUDE ~EET/other/EET_functions.tph~ //this function will be in the player's own EET folder
  LAF ~EET_NPC_TRANSITION~ 
    INT_VAR
      type = 1 //Biff is only available in BG:EE/SoD. Adapt this if needed; including the additional STR_VARs
    STR_VAR
      dv = "xxBiff" //Biff's scriptname (death variable) 
      override_BG1 = "" /* OVERRIDE script in BG:EE. Put your NPC's BG:EE script here UNLESS you use the same script for BG:EE and SoD 
(adds a "destruct in Chapter greater than 7" scriptblock, for example if your NPC was spawned in BG:EE but was never in party (although "BeenInParty" is not being checked by this scriptblock)). 
If your NPC is SoD only like Biff, leave this open. */
      override_SoD = "xxBiffs" //OVERRIDE script in SoD. This will add a "Selfdestruct" in Chapter greater than 13 to the SoD script.
      traFile = "%MOD_FOLDER%/tra/%LANGUAGE%/game.tra" //path to the text line for the ToB Fate Spirit summoning
      string = "@0" /* ~Bring me Biff the Understudy.~ */ //from the above specified tra file
      stringPosDV = "Baeloth" //Name after which Biff's should be put into the order of reply options at the fate spirit summoning dialogue. 
/* From the "Modder's Notes for Baldur's Gate: Enhanced Edition Trilogy (EET)":
    stringPosDV = "" //set the DV of NPC right below your response (used for sorting FATESP responses alphabetically)
      //Aerie, Ajantis, Alora, Anomen, Baeloth, Branwen, Cernd, Coran, Corwin, Dorn, Dynaheir, Edwin, Eldoth, Faldorn, Garrick,
      //Glint, HaerDalis, Hexxat, Imoen2, Jaheira, Jan, Kagain, Keldorn, Khalid, Kivan, Korgan, MKhiin, Mazzy, Minsc, Montaron,
      //Nalia, Neera, Quayle, Rasaad, Safana, SharTeel, Skie, Tiax, Valygar, Viconia, Voghiln, Wilson, Xan, Xzar, Yeslick, Yoshimo
      //variable not set (default) = NPC name appended at the end of summoning list */
  END
END



///////////////////////////////////////////////////////////////
/* Crossmod */
//////////////////////////////////////////////////////////////

BEGIN @90004 /* ~Biff the Tutorial SoD NPC: Crossmod Content~ */  
DESIGNATED 10 /* giving the component a DESIGNATED number which will not change in the future is of great importance for crossmod compatibility. In this case, "10" is an arbitrary number greater than "0" which is the main component and wasn't used for other components. Chose the number so it makes sense in your mod. */
LABEL xxbiff_sod_tutorial_mod-crossmod_content /* define a unique global label for this mod component(!). It basically does what DESIGNATED does, only better. And it helps Project Infinity remember the component when setting up an install list. */
/* make sure the component can only be installed if the main component is installed */
REQUIRE_COMPONENT ~biffsodtutorial.tp2~ ~0~  @90003 /* ~Biff main component needs to be installed for this.~ */
FORBID_COMPONENT ~biffsodtutorial.tp2~ ~100~ @90006 /* For compatibility reasons, if the crossmod contains NPC banter, the component "Use Dialogue Style for SoD Banters for Biff" needs to be installed after the crossmod component. */

/* SoD */

ACTION_IF (GAME_IS ~bgee eet~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN

/* Another fine Hell */
/* to make the tp2 less cluttered, I put the crossmod with Another fine Hell into another file: */
INCLUDE ~%MOD_FOLDER%/crossmod/crossmod_another_fine_hell.tpa~


/* The Boareskyr Bridge Scene */
ACTION_IF (MOD_IS_INSTALLED ~c#sodboabri/setup-c#sodboabri.tp2~ ~0~) THEN BEGIN
PRINT @90005 /* The Boareskyr Bridge Scene mod detected. */

// Get state for c#stff24 %after_incident_at_bridge_10% (in c#sodboabri/dialogues/bhaalheritage_boareskyr_bridge.d)
/* @76  = ~[FF Healer (female)]Everyone saw the sign of Bhaal painting itself on the ground around you. I fear what happened here will be the source of much speculation and rumors. Let us move on and leave this place.~
*/
OUTER_SET after_incident_at_bridge_10 = STATE_WHICH_SAYS 76 IN ~c#sodboabri/translations/autotra/%s/dialogues.tra~ FROM ~c#stff24~
  COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/crossmod/crossmod_boareskyrbridge.d~ 
     USING ~%MOD_FOLDER%/tra/%LANGUAGE%/game.TRA~
END

END //SoD, EET

///////////////////////////////////////////////////////////
// Use Dialogue Style for SoD Banters //
/////////////////////////////////////////////////////////

/* (T4) This is an optional component which will convert the banters into "dialogue style" */
BEGIN @90002 /* ~Use Dialogue Style for SoD Banters for Biff~ */
DESIGNATED 100 /* giving the component a DESIGNATED number which will not change in the future is of great importance for crossmod compatibility. In this case, "100" is an arbitrary number greater than "0" which is the main component and wasn't used for other components. Chose the number so it makes sense in your mod. */
LABEL xxbiff_sod_tutorial_mod-dialogue_style_sod_banter /* define a unique global label for this mod component(!). It basically does what DESIGNATED does, only better. And it helps Project Infinity remember the component when setting up an install list. */
/* make sure the component can only be installed if the main component is installed */
REQUIRE_COMPONENT ~biffsodtutorial.tp2~ ~0~  @90003 /* ~Biff main component needs to be installed for this.~ */
/* in case your main mod also covers other games than SoD, we include the SoD check again because this component only makes sense for SoD: */
REQUIRE_PREDICATE (GAME_IS ~bgee eet~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) @90001 /* ~This mod/component is really only for SoD~ */


/* (T4) SoD bdbanter.2da - create if not present and patch with Biff's entry */
ACTION_IF !(FILE_EXISTS_IN_GAME ~BDBANTER.2DA~) BEGIN
  COPY ~%MOD_FOLDER%/2da/BDBANTER.2DA~ ~override~ 
END
ACTION_IF (FILE_EXISTS_IN_GAME ~BDBANTER.2DA~) BEGIN
  APPEND ~bdbanter.2da~
	~xxBiff 	xxBiffB~ //replace with scriptname and banter file name of your NPC
  UNLESS ~xxBiff~        //replace with scriptname of your NPC  
END

/* (T4) Create NPCs' Banter files if not present */
COPY ~%MOD_FOLDER%/2da/BDBANTER.2DA~ ~%MOD_FOLDER%/install~
	COUNT_2DA_COLS cols
	COUNT_2DA_ROWS cols rows
	FOR ( row = 2 ; row < rows ; row = row + 1 ) BEGIN
		READ_2DA_ENTRY %row% 0 2 npc_dv
    	READ_2DA_ENTRY %row% 1 2 banterfile
      	INNER_ACTION BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~%banterfile%.dlg~ THEN BEGIN
		<<<<<<<< .../banterfiles-inlined/%banterfile%.d
		 BEGIN %banterfile%
		>>>>>>>>
		COMPILE EVALUATE_BUFFER ~.../banterfiles-inlined/%banterfile%.d~ 
		END
      	END
	END
BUT_ONLY

/* (T4) Compile the banters in d-file */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogue/banter_sod.d~

/* (T4) change the calling of the scripts to "Interact("npc") for all banters 
Code taken from AstroBryGuy's "SoD Dialog Banters" mod with many thanks! */
// Edit NPC scripts to start Interact actions for banters
COPY ~%MOD_FOLDER%/2da/xxBiffbx.2da~ ~%MOD_FOLDER%/install~
	COUNT_2DA_COLS cols
	COUNT_2DA_ROWS cols rows
	FOR ( row = 0 ; row < rows ; row = row + 1 ) BEGIN
		READ_2DA_ENTRY %row% 0 3 banter
    	READ_2DA_ENTRY %row% 1 3 script
    	READ_2DA_ENTRY %row% 2 3 interact
      	INNER_ACTION BEGIN
      		COPY_EXISTING ~%script%.BCS~ ~override~
    			DECOMPILE_AND_PATCH BEGIN
        			REPLACE_TEXTUALLY ~SetAreaScript("%banter%",GENERAL)~ ~Interact("%interact%")~
        		END
        	BUT_ONLY
      	END
	END
BUT_ONLY
